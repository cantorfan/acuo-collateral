plugins {
    id 'io.franzbecker.gradle-lombok' version '1.6'
}

apply from: "$rootDir/gradle/projects/java-plugins.gradle"
apply from: "$rootDir/gradle/projects/dependencies.gradle"
apply from: "$rootDir/gradle/projects/integration-test.gradle"
apply from: "$rootDir/gradle/projects/deployment.gradle"
apply from: "$rootDir/gradle/projects/sonarqube.gradle"

ext.versions += [
    common   : '0.2.1',
    neo4j    : '3.0.6',
    neo4jOgm : '2.0.5',
    neo4jBolt: '1.0.5',
    guice    : '4.0',
    jackson  : '2.8.3',
    resteasy : '3.0.19.Final',
    metrics : '3.1.2'
]

apply plugin: 'application'

mainClassName = 'com.acuo.collateral.DataApp'
applicationDefaultJvmArgs = ["-Dacuo.config.appid=${config.appId}",
                             "-Dacuo.config.env=${config.env}"]

distributions {
    main {
        contents {
            from('src/main/webapp') {
                into 'webapp'
            }
            from('src/main/resources/graph-data') {
                into 'webapp/graph-data'
            }
            from('src/main/resources/config') {
                into 'data/config'
            }
        }
    }
}

task printProperties << {
    println config.appId
    println config.env
    println versions.resteasy
    println versions.slf4j
    println versions.guice
    println versions.guava
    println versions.jackson
}

configurations {
    all*.exclude group: 'javax.ws.rs' , module: 'jsr311-api'
}

dependencies {
    compile(
            "com.acuo.collateral:common:${versions.common}",

            "org.neo4j:neo4j:${versions.neo4j}",
            "org.neo4j.driver:neo4j-java-driver:${versions.neo4jBolt}",

            "org.neo4j:neo4j-ogm-core:${versions.neo4jOgm}",
            "org.neo4j:neo4j-ogm-http-driver:${versions.neo4jOgm}",
            "org.neo4j:neo4j-ogm-bolt-driver:${versions.neo4jOgm}",
            "org.neo4j:neo4j-ogm-embedded-driver:${versions.neo4jOgm}",

            "com.google.inject:guice:${versions.guice}",
            "com.google.inject.extensions:guice-servlet:${versions.guice}",
            "com.google.inject.extensions:guice-persist:${versions.guice}",
            "com.google.inject.extensions:guice-assistedinject:${versions.guice}",

            "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:${versions.jackson}",
            "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${versions.jackson}",

            "org.jboss.resteasy:resteasy-client:${versions.resteasy}",
            "org.jboss.resteasy:resteasy-jaxrs:${versions.resteasy}",
            "org.jboss.resteasy:resteasy-guice:${versions.resteasy}",
            "org.jboss.resteasy:resteasy-jackson2-provider:${versions.resteasy}",

            "io.dropwizard.metrics:metrics-core:${versions.metrics}",
            "io.dropwizard.metrics:metrics-jvm:${versions.metrics}",
            "io.dropwizard.metrics:metrics-servlets:${versions.metrics}",
            "io.dropwizard.metrics:metrics-annotation:${versions.metrics}",

            "com.smokejumperit.guice:core:1.1",
            "io.jsonwebtoken:jjwt:0.6.0",
            "org.mindrot:jbcrypt:0.3m"
    )

    testCompile(
            "com.acuo.collateral:common:${versions.common}:tests",
            "org.neo4j.test:neo4j-harness:${versions.neo4j}",
            "org.neo4j:neo4j-ogm-test:${versions.neo4jOgm}",
            "org.glassfish.jersey.test-framework.providers:jersey-test-framework-provider-grizzly2:2.22.2",
            "com.jayway.jsonpath:json-path-assert:2.2.0"
    )
}